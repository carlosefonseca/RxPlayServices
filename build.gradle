// Top-level build file where you can add configuration options common to all sub-projects/modules.

// To avoid manually setting the same values in all Android modules, set the value on the root
// project and then reference this from the modules
ext {
    githubUrl = 'https://github.com/carlosefonseca/RxPlayServices'
    gitUrl = "${githubUrl}.git"
    gitTag = "v$version"

    projectFriendlyName = 'RxPlayServices'
    projectDescription = 'Library that wraps Google Play services APIs using RxJava'

    groupId = 'com.carlosefonseca.rxplayservices'
    versionName = '0.5.0.5'

    minSdkVersion = 11
    compileSdkVersion = 24
    targetSdkVersion = 24

    vGooglePlayServices = '11.0.4'
    vRxJava = '2.1.13'

    vTestSupportLib = '0.5'
}

buildscript {
    ext {
        vAndroidGradlePlugin = '3.1.2'
        vKotlin = '1.2.41'

        vDokkaPlugin = '0.9.15'
        vAndroidMavenGradlePlugin = '1.5'
        vGradleBintrayPlugin = '1.7.3'

        vDexcount = '0.6.2'
    }
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$vAndroidGradlePlugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$vKotlin"

        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:$vDokkaPlugin"
        classpath "com.github.dcendents:android-maven-gradle-plugin:$vAndroidMavenGradlePlugin"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$vGradleBintrayPlugin"
    }
}

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

def getArtifactId(String projectName) {
    return rootProject.name.toLowerCase() + (projectName == 'library' ? '' : ('-' + projectName))
}

allprojects {
    version = rootProject.ext.versionName
    group = rootProject.ext.groupId

    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}

task wrapper(type: Wrapper) {
    distributionUrl = "https://services.gradle.org/distributions/gradle-4.4-all.zip"
}
